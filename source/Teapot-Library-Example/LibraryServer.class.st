"
I'm the web server that provides a REST API for creating, deleting and finding books. My responses are in JSON format (generated by NeoJSON).
I use Teapot internally.
"
Class {
	#name : #LibraryServer,
	#superclass : #Object,
	#instVars : [
		'port',
		'teapot',
		'bookShelf'
	],
	#category : #'Teapot-Library-Example'
}

{ #category : #'instance creation' }
LibraryServer class >> serveOn: portNumber [
	^ self new  
		initializePort: portNumber;
		start
]

{ #category : #rest }
LibraryServer >> createBook: request [
	| book bookId |
	book := self makeBook: request.
	bookId := bookShelf addBook: book.
	^ TeaResponse created: '/books/', bookId asString
]

{ #category : #rest }
LibraryServer >> deleteBook: request [
	| bookId |
	bookId := request at: #id.
	^ [ 
		bookShelf removeBook: bookId.
		TeaResponse ok: 'Deleted /books/' , bookId asString 
	   ]
		on: BookNotFound
		do: [ TeaResponse code: 204 body: 'No content' ]
]

{ #category : #initialization }
LibraryServer >> initializePort: anInteger [
	bookShelf := BookShelf new.
	teapot := Teapot configure: { 
		#port -> anInteger. 
		#debugMode -> true. 
		#defaultOutput -> #json 
	}.
	self registerRoutes.	
	^ self
]

{ #category : #private }
LibraryServer >> makeBook: request [
	^ {#title   -> (request at: #title).
      #authors -> (request at: #authors)} asDictionary.
]

{ #category : #rest }
LibraryServer >> readBook: request [
	^ bookShelf findBook: (request at: #id)
]

{ #category : #rest }
LibraryServer >> readBooks: aTeaRequest [ 
	^ bookShelf books
]

{ #category : #initialization }
LibraryServer >> registerRoutes [
	teapot
		GET: '/books' -> (Send message: #readBooks: to: self);
		GET: '/books/<id:IsInteger>' -> (Send message: #readBook: to: self);
		POST: '/books' -> (Send message: #createBook: to: self);		
		PUT: '/books/<id:IsInteger>' -> (Send message: #updateBook: to: self);
		DELETE: '/books/<id:IsInteger>' -> (Send message: #deleteBook: to: self);
		exception: TeaNoSuchParam -> [ :ex :req | TeaResponse badRequest: req ];
		exception: BookNotFound -> [ :ex :req | TeaResponse notFound: '/books/', ex messageText ].
]

{ #category : #controlling }
LibraryServer >> start [
	teapot start.
	^ self
]

{ #category : #controlling }
LibraryServer >> stop [
	teapot stop
]

{ #category : #rest }
LibraryServer >> updateBook: request [
	| updatedBook |
	updatedBook := self makeBook: request.
	^ [ 
		bookShelf replaceBook: (request at: 'id') with: updatedBook.
		TeaResponse ok: 'updated'
  	   ]
		on: BookNotFound
		do: [ TeaResponse code: 204 body: 'No content' ]	
	

	
]
